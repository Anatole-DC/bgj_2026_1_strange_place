jump {GameState.get_current_day()}

# All dialogues for the first day
label FIRST
if {GameState.is_task_complete("intro_dialogue")} == false:
	a: Hey, P! Looks like we’ll need to call a contractor to install the isolating slip-bind on the CHEMICALNAME tanks.
	a: Could you approve our request before the end of the day?
	player: Let me check in with C, I’ll get back to you.
	do GameState.complete_task("intro_dialogue")
else:
	a: <C complains that there’s too many operators working the tanks … the ground floor operators complain that they don’t have enough people working the tanks … I just want to sleep./I wish I could go back and get it right./If I have to pick up another extra shift, I’ll collapse …/Why am I back in my office?>
[end_timeline]

# All dialogues for second day
label SECOND
if {GameState.is_task_complete("talk_with_a_about_repairs")} == false:
	a: Hey, I know we are over budget, but I heard that the trade unions have asked for another safety inspection.
	a: Could you call C and ask if we can get a contractor to repair the scrubbers?
	a: Or the pressure gauges? Or the flare stack? Or literally anything?
	player: I will try, but I think I know the answer.
	a: This is not great … TOWNNAME is really close.
	a: If something happens, I don't think the hospitals would know how to treat them … but, well. What can we do.
	player: We could warn the [color=red]Regional Health Office[/color] that CHEMICALNAME is a class one hazardous substance, and what it does to people.
	a: And lose our job? No thank you.
	do GameState.complete_task("talk_with_a_about_repairs")
else:
	a: <Don’t tell S, but I think the trade union was right to call the Factory Inspector. They did the right thing./I wish I could go back and get it right./If I have to pick up another extra shift, I’ll collapse …/Why am I back in my office?>
[end_timeline]

# All dialogues for the last day
label LAST
if {GameState.is_task_complete("talk_with_a_about_sirens")} == false:
	a: Hey, P. There’s rumors among the workers that C wants to reduce the ground floor operators. Could you call her and ask her for confirmation?
	player: Sure thing. I heard there was a leak yesterday night, and I live in TOWNNAME, but I didn’t hear the siren. Why is that?
	a: Oh, K asked us to decouple the sirens. You know, to not cause alarm.
	player: That doesn’t sound good. Is she sure?
	a: I told her, and she said she’s sure. You can always email management if you are worried.
	do GameState.complete_task("talk_with_a_about_sirens")
else:
	a: <I should have had more spine. I should have reconnected the sirens./I wish I could go back and get it right./I need a cigarette. And a coffee. Maybe both …/Why am I back in my office?  Why am I back in my office?>
[end_timeline]